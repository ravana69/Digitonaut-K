<!DOCTYPE html>
<html lang="en" >

<!-- Mirrored from digitonaut.com/art/kaleidolines/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 12:59:55 GMT -->
<!-- Added by HTTrack --><meta http-equiv="content-type" content="text/html;charset=UTF-8" /><!-- /Added by HTTrack -->
<head>
<meta charset='utf-8'\>
<meta name='apple-mobile-web-app-capable' content='yes'><meta name='apple-mobile-web-app-status-bar-style' content='black-translucent'><title>Kaleidolines</title>

<style>
canvas {
	position:fixed;
	left:50%; top:50%;
	transform:translate(-50%,-50%);
	border:0px; outline:0px;
}
</style>
</head>
<body style='margin:0px;overflow:hidden;background:#000;'>

<script type="application/processing">
final int NB_SYMETRIES = 6;
float R;//length of the triangles hypotenuse
float h, l, theta;//height, length, angle of the triangles
float rot = 0;//rotation of the texture-mapping triangle
PVector c;
PVector[] p = new PVector[4];
float sizeCoeff = 1;
Boolean displayTriangle = false;


MyNoise myNoise;

void setup(){
  size(screenWidth,screenHeight, P3D);
  background(0);
  noStroke();
  initialize();
}

void initialize(){
  R = random(max(width, height)/20, max(width, height)/2);

  theta = TWO_PI / NB_SYMETRIES;
  l = R*cos(theta/2);
  h = R*sin(theta/2);
  for (int i = 0; i < 3; i++){
    p[i] = new PVector(0, 0);
  }
  c = new PVector(width/2, height/2);
  myNoise  = new MyNoise();
}

void draw(){
  background(0);

  myNoise.update();
  sizeCoeff = map(mouseY, 0, height, min(1, min(width, height)/2/max(h, l)), min(min(width, height)/2/max(h, l), 3));
  rot = map(mouseX, 0, width, 0, TWO_PI);

  ////////////////////////////////////////////////////////////////////////////////
  ////////////////////////////////////////////////////////////////////////////////
  ////////////////////////////////////////////////////////////////////////////////
  p[0].x = c.x;
  p[0].y = c.y;
  p[1].x = c.x - sizeCoeff * h * sin(rot);
  p[1].y = c.y - sizeCoeff * h * cos(rot);
  p[2].x = c.x + sizeCoeff * l * cos(rot);
  p[2].y = c.y - sizeCoeff * l * sin(rot);
  ////////////////////////////////////////////////////////////////////////////////
  ////////////////////////////////////////////////////////////////////////////////
  ////////////////////////////////////////////////////////////////////////////////
  noStroke();
  PImage pim = myNoise.myPg.get();
  for (int v = 0; v < width / (2*h); v++){
    for (int w = 0; w < height / (2*l) + 1; w++){
      pushMatrix();
      translate(v * 3 * h, ((v % 2 == 0) ? 0 : l) + w * 2 * l);
      beginShape(TRIANGLES);
      texture(pim);
      for (int i = 0; i < NB_SYMETRIES; i++){
        vertex(0, 0, p[2].x, p[2].y);
        vertex(R*cos(i*TWO_PI/NB_SYMETRIES), R*sin(i*TWO_PI/NB_SYMETRIES), p[1].x, p[1].y);
        vertex(l*cos(i*TWO_PI/NB_SYMETRIES-theta/2), l*sin(i*TWO_PI/NB_SYMETRIES-theta/2), p[0].x, p[0].y);

        vertex(0, 0, p[2].x, p[2].y);
        vertex(R*cos((i+1)*TWO_PI/NB_SYMETRIES), R*sin((i+1)*TWO_PI/NB_SYMETRIES), p[1].x, p[1].y);
        vertex(l*cos((i+1)*TWO_PI/NB_SYMETRIES+theta/2), l*sin((i+1)*TWO_PI/NB_SYMETRIES+theta/2), p[0].x, p[0].y);
      }
      endShape();
      popMatrix();
    }
  }
  
  if(displayTriangle){  
    //draw the triangle that is used for the texturing
     noFill();
     stroke(120);
     beginShape(TRIANGLES);
     //texture(myNoise.myPg);
     for (int i = 0; i < 3; i++){
       vertex(p[i].x, p[i].y);
     }
     endShape();
  }
}

void mousePressed(){
  initialize();
}

void keyPressed(){
  displayTriangle = !displayTriangle;
}
class MyNoise
{
  final int NB_PARTICLES = 5000;
  final float MAX_PARTICLE_SPEED = 2.5;
  final float PARTICULE_SIZE = 1;

  final float MIN_STEP_NOISE = 0.004;
  final float MAX_STEP_NOISE = 0.01;
  final float MIN_SPEED_NOISE = -.02;
  final float MAX_SPEED_NOISE = .02;
  float noiseXY;//'root' of the noise
  float noiseSpeed;//used to move the noise - or not
  float stepNoiseXY;

  myVector tabParticles[];//array of particles

  float coeffColor;
  PGraphics myPg;//everything will be drawn on it

  MyNoise(){
    myPg = createGraphics(width, height, P2D);
 
    noiseXY = noiseXY = random(123456);
    noiseSpeed = random(MIN_SPEED_NOISE, MAX_SPEED_NOISE);
    //noiseSpeed = l/1000;
    coeffColor = random(.7, 2.3);
    stepNoiseXY = random(MIN_STEP_NOISE, MAX_STEP_NOISE);
    tabParticles = new myVector[NB_PARTICLES];
    for (int i = 0; i < NB_PARTICLES; i++){
      tabParticles[i] = new myVector(random(width), random(height));
      tabParticles[i].prevX = tabParticles[i].x;
      tabParticles[i].prevY = tabParticles[i].y;
      float n = noise(noiseXY+tabParticles[i].x*stepNoiseXY, noiseXY+tabParticles[i].y*stepNoiseXY);
      tabParticles[i].myColor = color((255-n*255)*coeffColor, 255-n*126*coeffColor, 255*n*coeffColor);
    }
  }

  public void update(){
    noiseXY += noiseSpeed;
    float n, vx, vy, dx, dy, distMouse;
    PVector mouseVector = new PVector(mouseX, mouseY);
    
    myPg.beginDraw();
    myPg.fill(0, 15);
    myPg.rect(0, 0, width, height);

    for (int i = 0; i < NB_PARTICLES; i++){
      myPg.fill(tabParticles[i].myColor);
      tabParticles[i].prevX = tabParticles[i].x;
      tabParticles[i].prevY = tabParticles[i].y;

      n = noise(noiseXY+tabParticles[i].x*stepNoiseXY, noiseXY+tabParticles[i].y*stepNoiseXY);

      vx = (1-n)*2*cos(n * TWO_PI)*MAX_PARTICLE_SPEED;
      vy = (1-n)*2*sin(n * TWO_PI)*MAX_PARTICLE_SPEED;

      vx = constrain(vx, -MAX_PARTICLE_SPEED, MAX_PARTICLE_SPEED);
      vy = constrain(vy, -MAX_PARTICLE_SPEED, MAX_PARTICLE_SPEED);

      tabParticles[i].x += vx;
      tabParticles[i].y += vy;

      if ((tabParticles[i].x < 0) || (tabParticles[i].x > width) || 
        (tabParticles[i].y < 0) || (tabParticles[i].y > height))
      {
        float myX = random(width);
        float myY = random(height);

        tabParticles[i].x = tabParticles[i].prevX = myX;
        tabParticles[i].y = tabParticles[i].prevY = myY;
      }
      tabParticles[i].myColor = color((255-n*255)*coeffColor, 255-n*126*coeffColor, 255*n*coeffColor);

      //myPg.strokeWeight(sqrt(vx*vx + vy*vy)*n*1.5);
      myPg.stroke(tabParticles[i].myColor, 150);
      myPg.line(tabParticles[i].prevX, tabParticles[i].prevY, tabParticles[i].x, tabParticles[i].y);
    }
    myPg.endDraw();
  }
}


class myVector extends PVector{
  myVector (float p_x, float p_y) {
    super(p_x, p_y);
  }
  float prevX;
  float prevY;
  color myColor;
}


</script>

<canvas> </canvas>
<script src='processing.min.js'></script>
</body>

<!-- Mirrored from digitonaut.com/art/kaleidolines/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 12:59:58 GMT -->
</html>
